<script src="//ajax.googleapis.com/ajax/libs/jquery/2.0.3/jquery.min.js"></script>
<script src="//cdnjs.cloudflare.com/ajax/libs/coffee-script/1.6.3/coffee-script.min.js"></script>
<script type="text/coffeescript">
	$.get(location.pathname).done (data) ->
		c = data.split('##\n')[1]
		eval CoffeeScript.compile(c)
		env.store 'fluxcode.coffee', c
##

window.env = (name) -> env._cb[name] ?= $.Callbacks()
$.extend env,
	_cb: {}
	_docs: {}

	space: (space) -> 
		if space
			window.location.hash = space
			location.reload()
		else
			location.hash[1..] or "home"
	
	spaces: -> 
		key.substr(6) for key of localStorage when key.indexOf("space.") == 0

	clear: (space) ->
		space ?= env.space()
		if confirm("Are you sure you want to delete "+space+"?")
			delete(localStorage["space."+space])
			location.reload()

	retrieve: (name) ->
		env.get(name)
	
	get: (name) ->
		env._docs[name] || ""

	store: (name, value) ->
		env.put(name, value)
	
	put: (name, value) ->
		env._docs[name] = value
		env('docs.change').fire(name)
	
	list: ->
		name for name of env._docs when name[0] != '.'
	
	delete: (name) ->
		delete(env._docs[name])
		env('docs.change').fire(name)

	load: (url) ->
		loaders[url.split('.').slice(-1)[0]](url)
	
	install: (name, url) ->
		if not env._docs[name]
			$.get(url).done (data) ->
				env.store name, data
				env.load name
		else
			env.load name
	
	init: ->
		window.document.title = "Fluxcode"
		window.onhashchange = -> location.reload()
		
		if localStorage["space."+env.space()]
			env._docs = JSON.parse(localStorage["space."+env.space()])
		else
			env._docs = {}
		env('docs.change').add ->
			localStorage["space."+env.space()] = JSON.stringify(env._docs)
		
		if env._docs['init.js']
			env.load('init.js')
		if env._docs['init.coffee']
			env.load('init.coffee')
		
		console.log "Fluxcode ready."

loaders =
	css: (url) ->
		d = $.Deferred()
		$("<link />", {href: url, type: "text/css", rel: "stylesheet"})
		.on("load", -> d.resolve())
		.appendTo(document.head)
		d

	js: (url) ->
		if env._docs[url]
			eval(env._docs[url])
			$.Deferred().resolve()
		else
			$.getScript(url)

	coffee: (url) ->
		if env._docs[url]
			eval(CoffeeScript.compile(env._docs[url]))
			$.Deferred().resolve()
		else
			d = $.Deferred()
			$.get(url).done (data) ->
				eval(CoffeeScript.compile(data))
				d.resolve()
			d

env.init()

#</script>